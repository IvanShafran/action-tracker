apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

// junit5 doesn't support android projects out of the box
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "me.shafran.actiontracker"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        junitPlatform {
            jupiterVersion junit5jupiterVersion

            vintageVersion junit5vintageVersion

            filters {
                engines {
                    include 'spek'
                    include 'junit-vintage'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    def compatVersion = "27.0.2"

    implementation "com.android.support:appcompat-v7:$compatVersion"
    implementation "com.android.support:cardview-v7:$compatVersion"
    implementation "com.android.support:recyclerview-v7:$compatVersion"
    implementation "com.android.support:design:$compatVersion"

    def moxyVersion = "1.4.6"

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"

    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.1.4"

    implementation 'ru.terrakok.cicerone:cicerone:3.0.0'

}

// Spek setup
dependencies {
    def spekVersion = "1.1.5"

    testImplementation("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: "org.junit.platform"
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.junit.platform:junit-platform-engine:${junit5junitPlatformVersion}"

    testImplementation junit5.unitTests()
    // For running tests inside Android Studio 3.x
    testCompileOnly junit5.unitTestsRuntime()
}

// Mocking
dependencies {
    def powerMockVersion = "1.7.3"
    testImplementation 'org.mockito:mockito-core:2.10.0'

    testImplementation "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powerMockVersion}"
}

// Robolectric
dependencies {
    testImplementation "org.robolectric:robolectric:3.6.1"
}
