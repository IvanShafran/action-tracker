apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

// junit5 doesn't support android projects out of the box
apply plugin: 'de.mannodermaus.android-junit5'

apply from: 'action-tracker-signing.gradle'

apply plugin: 'jacoco'

android {
    defaultConfig {
        applicationId "me.shafran.actiontracker"
        minSdkVersion 21
        compileSdkVersion 28
        targetSdkVersion 28
        buildToolsVersion "28.0.2"
        versionCode 2
        versionName "0.2"

        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: "me.shafran.actiontracker"]
            }
        }

        resConfig 'en'
    }

    buildTypes {

        debug {
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        junitPlatform {
            jupiterVersion junit5jupiterVersion

            vintageVersion junit5vintageVersion

            filters {
                engines {
                    include 'spek'
                    include 'junit-vintage'
                }
            }

            jacocoOptions {
                excludedClasses += [
                        // Moxy
                        'com/arellomobile/mvp/*',
                        '**/*$$State*',
                        '**/*$$PresentersBinder*',
                        '**/*$$ViewStateProvider*',
                        // Toothpick
                        '**/*$$Factory.class',
                        '**/MemberInjectorRegistry.class',
                        '**/FactoryRegistry.class'
                ]
                taskGenerationEnabled = true
                onlyGenerateTasksForVariants "debug"
                xml.enabled true
                html.enabled true
                csv.enabled true
            }
        }
    }

    lintOptions {
        lintConfig file("../lint.xml")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def compatVersion = "27.1.1"

    implementation "com.android.support:appcompat-v7:$compatVersion"
    implementation "com.android.support:cardview-v7:$compatVersion"
    implementation "com.android.support:recyclerview-v7:$compatVersion"
    implementation "com.android.support:design:$compatVersion"

    def moxyVersion = "1.4.6"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"

    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.1"

    implementation 'ru.terrakok.cicerone:cicerone:3.0.0'

    def toothpickVersion = "1.1.3"
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpickVersion"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpickVersion"

    implementation "ru.terrakok.cicerone:cicerone:3.0.0"

    implementation "com.github.sundeepk:compact-calendar-view:3.0.0"
}

// Spek setup
dependencies {
    def spekVersion = "1.1.5"

    testImplementation("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: "org.junit.platform"
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.junit.platform:junit-platform-engine:${junit5junitPlatformVersion}"

    testImplementation junit5.unitTests()
}

// Mocking
dependencies {
    testImplementation 'org.mockito:mockito-core:2.21.0'
}

// Robolectric
dependencies {
    testImplementation "org.robolectric:robolectric:3.8"
}
